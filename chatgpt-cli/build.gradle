plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.graalvm.buildtools.native' version '0.9.20'
}

sourceCompatibility = '17'

repositories {
    mavenCentral()
}

ext {
    set('springShellVersion', "3.0.0")
    gitSha = 'git rev-parse --verify HEAD'.execute().text.trim()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation project(":chatgpt-java-sdk")

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.shell:spring-shell-starter'

    implementation 'org.fusesource.jansi:jansi:2.4.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

bootBuildImage {
    var image = "${System.getenv('DOCKER_REGISTRY') ?: 'kezhenxu94'}/${project.name}"
    imageName = image
    tags = ["latest", project.ext.gitSha].collect { "$image:$it" }

    docker {
        publishRegistry {
            username = System.getenv('DOCKER_USERNAME') ?: project.properties.dockerUsername
            password = System.getenv('DOCKER_PASSWORD') ?: project.properties.dockerPassword
        }
    }
}
